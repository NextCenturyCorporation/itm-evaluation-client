buildscript {
  repositories {
    gradlePluginPortal()
  }
  dependencies {
    classpath("org.openapi.generator:org.openapi.generator.gradle.plugin:7.11.0")
  }
}
apply plugin:'base'
apply plugin: 'org.openapi.generator'

defaultTasks 'clean', 'swaggerClean', 'determineDomain', 'generate', 'postGeneration'

def generatedApiDirectory = "generated"
def apiDirectory = "swagger"
def outputDirectory = "swagger_client"
def baseSwaggerFile = "base_swagger.yaml"
def domainSwaggerFile = "domain_swagger.yaml"
def domain = "p2triage" // Default domain
def testDirectory = "test"
def docsDirectory = "docs"
def readmeDest = "${projectDir}"
def apiFilename = "itm_ta2_eval_api.py"

task swaggerClean(type: Delete) {
    doFirst {
        delete generatedApiDirectory
    }
    mustRunAfter "clean"
}

task determineDomain {
    if (project.hasProperty("domain")) {
        domain = project.getProperty("domain")
        println "Setting domain to '${domain}'."
    }
    copy {
        from "${apiDirectory}/${domain}_swagger.yaml"
        into "${apiDirectory}"
        rename { String filename ->
            "${domainSwaggerFile}"
        }
    }
}

// Define a task for validating the primary YAML
task validate(type: org.openapitools.generator.gradle.plugin.tasks.ValidateTask) {
    inputSpec.set("$projectDir/$apiDirectory/$baseSwaggerFile")
}

// Define a task for validating the domain-specific YAML in isolation
task validateDomainSpec(type: org.openapitools.generator.gradle.plugin.tasks.ValidateTask) {
    dependsOn "determineDomain"
    inputSpec.set("$projectDir/$apiDirectory/$domainSwaggerFile")
}

// Define a task to clean then generate
task generate {
    dependsOn "openApiGenerate"
    mustRunAfter "determineDomain"
}

openApiGenerate {
    generatorName = "python"
    inputSpec = "$projectDir/$apiDirectory/base_swagger.yaml".toString()
    outputDir = "$projectDir/$generatedApiDirectory".toString()
    ignoreFileOverride = "$projectDir/.openapi-generator-ignore".toString()
    packageName = "$outputDirectory".toString()
    globalProperties = [
            modelDocs: "true"
    ]
    skipValidateSpec = true
    logToStderr = true
    generateAliasAsModel = false
    // set to true and set environment variable {LANG}_POST_PROCESS_FILE
    // (e.g. SCALA_POST_PROCESS_FILE) to the linter/formatter to be processed.
    // This command will be passed one file at a time for most supported post processors.
//    enablePostProcessFile = false
}

task postGeneration() {
    mustRunAfter "openApiGenerate"
    doLast() {
        // Copy generated files into models folder
        copy {
            from "${generatedApiDirectory}/${outputDirectory}/models"
            into "${outputDirectory}/models"
        }

        copy {
            from "${generatedApiDirectory}/${testDirectory}/"
            into testDirectory
        }

        copy {
            from "${generatedApiDirectory}/${docsDirectory}/"
            into docsDirectory
        }

        copy {
            from "${generatedApiDirectory}/${outputDirectory}/api/${apiFilename}"
            into "${outputDirectory}/api"
        }

        copy {
             from "${generatedApiDirectory}/README.md"
             into readmeDest
             rename { String filename ->
                "README-API.md"
             } 
        }

        // delete generated api directory
        delete generatedApiDirectory
        println "removed ${generatedApiDirectory}"
    }
}

// Ensures tasks are re-run always, needed for swaggerCodeGen
gradle.taskGraph.whenReady { taskGraph ->
  taskGraph.getAllTasks().each {
      it.setOnlyIf { true }
      it.outputs.upToDateWhen { false }
  }
}